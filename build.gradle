buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'me.tatarka:gradle-retrolambda:3.2.0'
	}
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'

ext.configFile = file('build.properties')

ext.config = parseConfig(configFile)
ext.priv = parseConfig(file('private.properties'))

group = "com.elytradev"
archivesBaseName = "Correlated"
version = config.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

retrolambda {
	javaVersion JavaVersion.VERSION_1_6
	defaultMethods true
}

repositories {
	maven {
		name = 'sonatype-nexus'
		url = 'https://oss.sonatype.org/content/repositories/public/'
	}
	maven {
		name = 'elytra'
		url = 'http://repo.elytradev.com/'
	}
	maven {
		name = 'jei'
		url = 'http://dvs1.progwml6.com/files/maven'
	}
	maven {
		name = 'epoxide'
		url = 'http://maven.epoxide.org'
	}
	ivy {
		artifactPattern "http://unascribed.com/jars/[organisation]/[module].[ext]"
	}
	jcenter()
}

configurations {
	deploy
}

jar {
	classifier = 'slim'
	manifest {
		attributes (
			'FMLAT': 'correlated_at.cfg'
		)
	}
}

shadowJar {
	classifier = ''
	relocate 'com.elytradev.concrete', 'com.elytradev.correlated.repackage.com.elytradev.concrete'
	relocate 'org.commonmark', 'com.elytradev.correlated.repackage.org.commonmark'
	configurations = [project.configurations.shadow]
}

reobf {
	shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
	archives shadowJar
}

dependencies {
	deploy 'org.apache.maven.wagon:wagon-ssh:2.10'
	
	shadow 'com.elytradev:concrete:0.1.0'
	compile 'com.elytradev:concrete:0.1.0'
	
	shadow 'com.atlassian.commonmark:commonmark:0.9.0'
	compile 'com.atlassian.commonmark:commonmark:0.9.0'
	
	shadow 'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.9.0'
	compile 'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.9.0'
	
	shadow 'com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:0.9.0'
	compile 'com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:0.9.0'
	
	compile 'com.elytradev:probedataproviderapi:MC1.11.2_ver1.1.1'
	
	compile 'mezz.jei:jei_1.11.2:4.2.6.235:api'
	runtime 'mezz.jei:jei_1.11.2:4.2.6.235'
	
	compile 'net.industrial-craft:industrialcraft-2-2.6.173-ex110-api'
	compile 'com.aidancbrady:mekanism-mdk-1.10.2-9.2.1.300'
	compile 'com.mod-buildcraft:buildcraft-api-8.0.0-f0a4d6cf'
	
	compile 'net.darkhax.tesla:Tesla:1.11-1.3.0.51'
}

minecraft {
	version = config.forge
	mappings = config.mappings

	runDir = "minecraft"

	replaceIn 'src/main/java/com/elytradev/correlated/Correlated.java'
	replace '@VERSION@', project.version
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	
	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
		exclude '*.xcf'
		exclude '*.wav'
		exclude '*.flac'
	}
}

def parseConfig(File config) {
	if (!config.exists()) return new Properties();
	config.withReader {
		def prop = new Properties()
		prop.load(it)
		return (new ConfigSlurper().parse(prop))
	}
}

if (file("private.properties").exists()) {
	uploadArchives {
		repositories.mavenDeployer {
			configuration = configurations.deploy
			pom.artifactId = 'correlated'
			repository(url: "sftp://"+priv.sftpHost+priv.sftpPath) {
				authentication(userName: priv.sftpUser, privateKey: file(priv.sftpKey))
			}
		}
	}
}
